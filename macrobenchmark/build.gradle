plugins {
    id 'com.android.test'
    id 'androidx.baselineprofile'
    id 'org.jetbrains.kotlin.android'
}

// Best articles about this:
// - [https://stefma.medium.com/sourcecompatibility-targetcompatibility-and-jvm-toolchains-in-gradle-explained-d2c17c8cff7c]
// - [https://developer.android.com/build/jdks#source-compat]
kotlin {
    jvmToolchain(17)
}

android {
    namespace 'ir.erfansn.nsmavpn.baselineprofile'
    compileSdk 34

    defaultConfig {
        minSdk 28

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    targetProjectPath = ":app"
}

dependencies {
    implementation 'androidx.test:rules:1.5.0'
    implementation 'androidx.test.ext:junit:1.1.5'
    implementation 'androidx.test.uiautomator:uiautomator:2.3.0'
    implementation 'androidx.benchmark:benchmark-macro-junit4:1.2.3'
}

androidComponents {
    onVariants(selector().all()) { v ->
        def artifactsLoader = v.artifacts.getBuiltArtifactsLoader()
        v.instrumentationRunnerArguments.put(
                "targetAppId",
                v.testedApks.map { artifactsLoader.load(it)?.applicationId }
        )
    }
}
