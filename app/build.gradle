plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id 'com.google.protobuf'
    id 'io.gitlab.arturbosch.detekt'
}

android {
    namespace 'ir.erfansn.nsmavpn'
    compileSdk 33

    defaultConfig {
        applicationId 'ir.erfansn.nsmavpn'
        minSdk 21
        targetSdk 33
        versionCode 1
        // TODO: Change to Calender versioning
        versionName '1.0'

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.1'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1,DEPENDENCIES}'
            excludes += '/mozilla/public-suffix-list.txt'
        }
    }
}

dependencies {
    def composeBom = platform('androidx.compose:compose-bom:2023.04.00')
    implementation composeBom
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.material3:material3-window-size-class:1.0.1'
    implementation 'androidx.compose.ui:ui-tooling-preview'

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-compose:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.6.1'
    implementation 'androidx.activity:activity-compose:1.7.2'
    implementation 'androidx.datastore:datastore:1.0.0'
    implementation 'androidx.startup:startup-runtime:1.1.1'
    implementation 'androidx.core:core-splashscreen:1.0.1'
    implementation 'androidx.constraintlayout:constraintlayout-compose:1.0.1'
    implementation 'androidx.work:work-runtime-ktx:2.7.1'

    implementation 'androidx.hilt:hilt-work:1.0.0'
    kapt 'androidx.hilt:hilt-compiler:1.0.0'

    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    implementation 'com.google.accompanist:accompanist-systemuicontroller:0.28.0'

    implementation 'com.google.protobuf:protobuf-javalite:3.21.12'
    implementation 'com.google.protobuf:protobuf-kotlin-lite:3.21.12'

    implementation 'com.google.android.gms:play-services-auth:20.4.1'

    implementation 'com.google.api-client:google-api-client:2.0.0'
    implementation 'com.google.api-client:google-api-client-android:2.0.0'
    implementation 'com.google.apis:google-api-services-gmail:v1-rev20220404-2.0.0'

    implementation 'it.skrape:skrapeit:1.2.2'
    implementation 'io.coil-kt:coil-compose:2.2.2'

    testImplementation 'junit:junit:4.13.2'
    testImplementation "com.google.truth:truth:1.1.5"
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.10.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation composeBom
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    androidTestImplementation 'com.google.truth:truth:1.1.5'

    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'

    // TODO: Add twitter compose detekt lint
    // detekt 'io.gitlab.arturbosch.detekt:detekt-cli:1.22.0'
    // detektPlugins 'com.twitter.compose.rules:detekt:0.0.26'
}

kapt {
    correctErrorTypes true
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.21.12'
    }

    // Generates the java Protobuf-lite code for the Protobufs in this project. See
    // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
    // for more information.
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
                kotlin {
                    option 'lite'
                }
            }
        }
    }
}
